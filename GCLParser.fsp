// Open the file that defines the type "Expr" we shall use as AST
%{
open GCLTypesAST
%}

// Declare the tokens (terminal symbols)
%token <int> NUM 
%token <string> VAR
%token  IFELSE ASS SKIP SEMI IF FI DO OD ARROW LBRAC RBRAC TIMES DIV PLUS MINUS POW LPAR RPAR SQRT TRUE FALSE OR SCOR AND SCAND NUT EQUAL NEQ GT LT GEQ LEQ EOF DFLAG NDFLAG PFLAG STEPFLAG SAFLAG SECFLAG FLFLAG
// NOTE: the actual formats of these tokens are defined in the lexer file
//       as regular expressions

// Specify precedence and associativity of operators
// Precedence is given by the order (from low to high)
//%left PLUS MINUS
//%left TIMES DIV
//%right POW

// We declare the initial non-terminal symbol
%start start

// We specify the return type of each of then non-terminal symbols
%type <flag*Com> start
%type <Com> c
%type <gc> gc 
%type <gc> gc2 
%type <flag> f

%type <BoolExpr> b
%type <BoolExpr> b1
%type <BoolExpr> b2
%type <BoolExpr> b3

%type <Expr> a
%type <Expr> a1
%type <Expr> a2
%type <Expr> a3

// Grammar productions
%%

// The first production in "formal" notation is
// start -> C
// here written:
start: 
     | f c EOF             { ($1,$2) }
     | c EOF              { (PFlag ,$1) }

// Note that we need to insert an End-Of-File (EOF)
// The code annotation { $1 } specifies that parsing with this production
// returns whatever parsing the expression returns: $1 is the result of parsing
// the first component of the production (i.e. expression)

// The productions for exps
// written in the yacc format:
//C -> x := a | A[a] := a | skip | C ; C | if GC fi | do G
 c:
  | VAR ASS a                     {AssCom($1,$3)}
  | VAR LBRAC a RBRAC ASS a       {AssArrayCom($1,$3,$6)}
  | SKIP                          {SkipCom}
  | c SEMI c                      {SemiCom($1,$3)}
  | IF gc FI                      {IfCom($2)}
  | DO gc OD                      {DoCom($2)}


f:
  | DFLAG                         {DFlag}
  | NDFLAG                        {NDFlag}
  | PFLAG                         {PFlag}
  | STEPFLAG                      {StepFlag}
  | SAFLAG                        {SAFlag}
  | SECFLAG                       {SECFlag}
  | FLFLAG                        {FlemmingFlag}

//GC -> b -> C | GC [] GC
gc:
  |gc2 IFELSE gc                {IfElseGc($1,$3)}
  |gc2                          {$1}

gc2:
  |b ARROW c                    {ArrowGc($1,$3)}        



//a0 -> a0 + a1 | a0 - a1 | a1 
a:
  | a PLUS a1    { PlusExpr($1,$3) }
  | a MINUS a1   { MinusExpr($1,$3) }
  | a1                    { $1 }
//A1 -> A1 * A2 | A1 / A2 | A2
a1:
  | a1 TIMES a2   { TimesExpr($1,$3) }
  | a1 DIV a2     { DivExpr($1,$3) }
  | a2                     { $1 }
//A2 -> A2^A3
a2:
  | a3 POW a2     { PowExpr($1,$3) }
  | a3                     { $1 }
//A3 -> - A3 | + A3 | (E) | Num
a3:
  | PLUS a3              { UPlusExpr($2) }
  | MINUS a3             { UMinusExpr($2) }
  | NUM                  { Num($1) }
  | VAR                  { Var($1) }
  | VAR LBRAC a RBRAC    { Array($1, $3)}
  | LPAR a RPAR          { $2 }

//b -> true | false | b & b | b | b | b && b | b || b | !b| a = a | a != a | a > a | a >= a | a < a | a <= a 
b:
  | b OR b1               { OrBool ($1,$3) }
  | b SCOR b1             { ScorBool ($1,$3) }
  | b1                    { $1 }

b1:
  | b1 AND b2               {AndBool ($1,$3)}
  | b1 SCAND b2             {ScandBool ($1,$3)}
  | b2                      { $1 }

b2:
  | NUT b3              {NutBool ($2)}
  | b3                  { $1 }

b3:
  | a EQUAL a      {EqualBool ($1, $3)}
  | a NEQ a        {NeqBool ($1,$3)}
  | a LT a         {LtBool ($1,$3)}
  | a GT a         {GtBool ($1,$3)}
  | a GEQ a        {GeqBool ($1,$3)}
  | a LEQ a        {LeqBool ($1,$3)}
  | TRUE           {TrueBool}
  | FALSE          {FalseBool}
  | LPAR b RPAR    {$2}

// Again, the code annotation specifies the result of parsing
// For example { TimesExpr($1,$3) } specifies that parsing with the production
// returns the value TimesExpr($1,$3), where $i is the result of parsing
// component i in the production (in this case the lhs and rhs operands)

